managed implementation in class zbp_r_incidents_fjcm unique;
strict ( 2 );
with draft;

// First Behavior for Incidents
define behavior for z_r_incidents_fjcm alias Incidents
persistent table zdt_inct_fjcm  // PROBLEMA MAPEAR CRITICAL PRORITY
draft table zdt_inct_fjcm_d

etag master LocalLastChangedAt
lock master total etag LastChangedAt

authorization master ( global, instance )    // Authorization to operate just one or multiple(global) instances

{
  create ( authorization : global );
  update;
  delete;

  association _History { create ( features : instance ); with draft; } // Association to the History CDS
  association _Attachments { create ( features : instance ); with draft; } //


  field ( readonly )
  IncUUID,
  IncidentID,
  Status,
  ChangedDate,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  field ( numbering : managed )      // Managed numbering for IncidentID
  IncUUID;

  field (mandatory)                  // Mandatory fields for Incident according to requirements
   title,
   Description,
   Priority;

  action ( features : instance, authorization : update ) changeStatus
    parameter z_ae_change_status_fjcm result [1] $self;  // Parameters from the abstract CDS * Result self update and gives back the instance

  side effects { action changeStatus affects $self; }

  internal action setHistory;

  determination setDefaultHistory on save { create; }
  determination setDefaultValues on modify { create; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  mapping for zdt_inct_fjcm
    {
      IncUUID            = inc_uuid;
      IncidentID         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

// Second Behavior for Incidents

define behavior for z_dd_inct_h_fjcm alias History
implementation in class zbp_dd_inct_h_fjcm unique
persistent table zdt_inct_h_fjcm
draft table zdt_inct_h_fjcmd

lock dependent by _Incidents           // Locking dependent on the Incidents
authorization dependent by _Incidents  // Authorization dependent on the Incidents parent association
etag master LocalLastChangedAt
{
  update;
  delete;

  association _Incidents { with draft; }

  field ( numbering : managed )
  HisUUID;

  field ( readonly )
  HisUUID,
  IncUUID,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  mapping for zdt_inct_h_fjcm
    {
      HisUUID            = his_uuid;
      IncUUID            = inc_uuid;
      HisID              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      Text               = text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

// Attachment
define behavior for Z_R_ATTACHMENT_INCIDENTS_FJCM alias Attachments
implementation in class zbp_r_attach_incidents_fjcm unique
persistent table zdt_attach_fjcm
draft table zdt_attach_fjcmd
etag master LocalLastChangedAt
lock dependent by _Attachment
authorization dependent by _Attachment
{
  field ( mandatory : create )
   FileName, FileContent;

  field ( readonly )
   IncUUID, UploadDate, UploadedBy, LocalCreatedBy, LocalCreatedAt, LocalLastChangedBy, LocalLastChangedAt, LastChangedAt;

  field ( numbering : managed, readonly )
  AttachmentUUID;

  field ( readonly : update )
   FileName, FileContent;

  update;
  delete;

  determination setAttachmentDefaults on modify { create; }

  association _Attachment { with draft; }

  mapping for zdt_attach_fjcm
  {
    AttachmentUUID = attachment_uuid;
    IncUUID = inc_uuid;
    FileName = file_name;
    FileType = file_type;
    FileSize = file_size;
    MimeType = mime_type;
    FileContent = file_content;
    UploadDate = upload_date;
    UploadedBy = uploaded_by;
    LocalCreatedBy = local_created_by;
    LocalCreatedAt = local_created_at;
    LocalLastChangedBy = local_last_changed_by;
    LocalLastChangedAt = local_last_changed_at;
    LastChangedAt = last_changed_at;
  }
}